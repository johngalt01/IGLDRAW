0  ' IBM IGL Image Generation Language Picture Display Program
10 ' By John Galt for MBASIC and GEOFF Terminal: 4/15/2025
11 DEF FNLTRIM$ (X$)= RIGHT$(X$, (LEN(X$)-1)) 'STRIP THE LEADING SPACE STR CONVERT
12 DEF FNDRAWLON$ (X1$,Y1$,X2$,Y2$) = CHR$(27)+"[Z1;"+X1$+";"+Y1$+";"+X2$+";"+Y2$+"Z" ' LINE ON (X1,Y1,X2,Y2)
13 DEF FNDRAWLOFF$ (X1$,Y1$,X2$,Y2$)= CHR$(27)+"[Z6;"+X1$+";"+Y1$+";"+X2$+";"+Y2$+"Z" ' LINE OFF (X1,Y1,X2,Y2)
14 HOME$=CHR$(27)+"[H"
15 CLS$=CHR$(27)+"[2J"
16 CURSORON$=CHR$(27)+"[?25h"
17 CURSOROFF$=CHR$(27)+"[?25l"
20 PRINT CHR$(27)+"[2J"+CHR$(27)+"[H"; ' CLEAR SCREEN
30 PRINT "All IGL Images Files Found on Disk:":PRINT
40 FILES "*.IGL":PRINT
50 PRINT:INPUT "Enter The NAME of IGL file to display, no extension needed: ",IGLF$
60 INPUT "ENTER SCALE: ",S
70 INPUT "ENTER X OFFSET: ",MX
80 INPUT "ENTER Y OFFSET: ",MY
90 PRINT CLS$;HOME$ ' CLEAR SCREEN
110 OPEN "I",1,IGLF$+".IGL"
120 INPUT #1,N$
130 Q=1024/480:R=768/432
135 PRINT CURSOROFF$;

140 IF EOF(1) THEN 320 ' MAIN LOOP
150 INPUT #1,L$
155 IF LEN(L$)<11 OR LEN(L$)>12 THEN PRINT" ERROR: " L$ 
180 IF LEN(L$)=12 THEN GOTO 220 
190 I=VAL(LEFT$(L$,2)) ' LENGTH 11                      
200 X=VAL(MID$(L$,2,5)):Y=VAL(MID$(L$,8,4))
210 GOTO 250 
220 I=VAL(LEFT$(L$,2)) ' LENGTH 12 
230 X=VAL(MID$(L$,3,5)):Y=VAL(MID$(L$,9,4)) 
250 X=(X/Q)*S-MX
260 Y=(Y/R)*S-MY
270 Y=224-Y
275 PRINT CHR$(27)+"[H";
280 IF I=-1 THEN Q=Q*4:R=R*4:MX=MX/4:MY=MY/4:I=1:GOTO 400
290 IF I=1 THEN PREX=X:PREY=Y
300 IF I=0 THEN PRINT HOME$+FNDRAWLON$(FNLTRIM$(STR$(INT(PREX))),FNLTRIM$(STR$(INT(PREY))),FNLTRIM$(STR$(INT(X))),FNLTRIM$(STR$(INT(Y))));:PREX=X:PREY=Y
305 COMMAND$=INKEY$: IF COMMAND$="Q" THEN GOTO 320
307 Z=FRE("")
310 GOTO 140

320 CLOSE #1
325 PRINT CURSORON$;
330 PRINT CHR$(27)+"[35;26f";:PRINT "PRESS 'ESC' KEY TO RETURN TO CP/M";
350 WHILE INKEY$<>CHR$(27):WEND
360 PRINT CLS$;HOME$;
380 END

400 IF LEN(L$)=11 THEN GOTO 200
410 IF LEN(L$)=12 THEN GOTO 230

