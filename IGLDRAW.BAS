0  ' IBM IGL Image Generation Language Picture Display Program
10 ' By John Galt for MBASIC and FABGL ANSI Terminal: 4/13/2025
20 PRINT CHR$(27)+"[2J"+CHR$(27)+"[H"; ' CLEAR SCREEN
30 PRINT "All IGL Images Files Found on Disk:":PRINT
40 FILES "*.IGL":PRINT
50 PRINT:INPUT "Enter The NAME of IGL file to display, no extension needed: ",IGLF$
60 INPUT "ENTER SCALE: ",S
70 INPUT "ENTER X OFFSET: ",MX
80 INPUT "ENTER Y OFFSET: ",MY
90 PRINT CHR$(27)+"_E0":PRINT CHR$(27)+"[2J"+CHR$(27)+"[H"; ' CLEAR SCREEN
100 PRINT CHR$(27)+"_GPEN255;255;255"
110 OPEN "I",1,IGLF$+".IGL"
120 INPUT #1,N$
130 Q=1024/512:R=768/384

140 IF EOF(1) THEN 320 ' MAIN LOOP
150 INPUT #1,L$
155 IF LEN(L$)<11 OR LEN(L$)>12 THEN PRINT" ERROR: " L$ 
180 IF LEN(L$)=12 THEN GOTO 220 
190 I=VAL(LEFT$(L$,2)) ' LENGTH 11                      
200 X=VAL(MID$(L$,2,5)):Y=VAL(MID$(L$,8,4))
210 GOTO 250 
220 I=VAL(LEFT$(L$,2)) ' LENGTH 12 
230 X=VAL(MID$(L$,3,5)):Y=VAL(MID$(L$,9,4)) 
250 X=(X/Q)*S-MX
260 Y=(Y/R)*S-MY
270 Y=224-Y
275 PRINT CHR$(27)+"[H";
280 IF I=-1 THEN Q=Q*4:R=R*4:MX=MX/4:MY=MY/4:I=1:GOTO 400
290 IF I=1 THEN PRINT CHR$(27)+"_GPEN0;0;0":PRINT CHR$(27)+"_GLINE"STR$(INT(X))";"STR$(INT(Y))";"STR$(INT(X))";"STR$(INT(Y))"$":PREX=X:PREY=Y
300 IF I=0 THEN PRINT CHR$(27)+"_GPEN255;255;255":PRINT CHR$(27)+"_GLINE"STR$(INT(PREX))";"STR$(INT(PREY))";"STR$(INT(X))";"STR$(INT(Y))"$":PREX=X:PREY=Y
305 COMMAND$=INKEY$: IF COMMAND$="Q" THEN GOTO 320
310 GOTO 140

320 CLOSE #1
330 PRINT CHR$(27)+"[H";:PRINT CHR$(27)+"_F27;46":PRINT CHR$(27)+"_GPEN255;255;255"
340 PRINT "PRESS 'ESC' KEY TO RETURN TO CP/M";
350 WHILE INKEY$<>CHR$(27):WEND
360 PRINT CHR$(27)+"[2J"+CHR$(27)+"[H";
370 PRINT CHR$(27)+"_E1"
380 END

400 IF LEN(L$)=11 THEN GOTO 200
410 IF LEN(L$)=12 THEN GOTO 230

